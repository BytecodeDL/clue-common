plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
    id "com.jfrog.bintray" version "1.8.4"
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

project.group = 'org.clyze'

wrapper {
    gradleVersion = '6.2'
}

repositories {
    mavenCentral()
}

def groovyVersion='2.5.10'
dependencies {
    implementation "org.codehaus.groovy:groovy:${groovyVersion}"
    implementation "org.codehaus.groovy:groovy-xml:${groovyVersion}"
    api 'commons-io:commons-io:2.4'             // Apache 2.0
    api 'log4j:log4j:1.2.14'                    // Logging implementation, Apache 2.0
    implementation 'org.apache.ivy:ivy:2.4.0'   // Apache Ivy (for downloading inputs from Maven repos), Apache 2.0
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'  // MPL 1.1

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc {
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
        }
    }
}

release {
    //use defaults, see https://github.com/researchgate/gradle-release
}

tasks.release.doLast {
    println "WARNING: Releasing clue-common affects both public doop and doop-nexgen, please check that everything works."
}

afterReleaseBuild.dependsOn publish

if (project.hasProperty('artifactory_user')) {
    publishing {
        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }
                //Always publish to the public releases repo
                url "${artifactory_contextUrl}/libs-public-release-local"
            }
        }
    }
}

// Upload to bintray with:
//   ./gradlew clean build bintrayUpload --info
// Needs properties: bintray_user, bintray_key.
if (project.hasProperty('bintray_user') && project.hasProperty('bintray_key')) {
    bintray {
        user = bintray_user
        key = bintray_key
        publications = ['mavenJava']
        configurations = ['archives']
        pkg {
            repo = 'plast-lab'
            name = 'clue-common'
            desc = 'Common functionality for static analysis tools developed by the PLAST research group.'
            licenses = ['MIT']
            labels = ['groovy', 'java']
            vcsUrl = 'https://bitbucket.org/yanniss/clue-common.git'
            version {
                name = project.version
                released = new Date()
                vcsTag = project.version
            }
        }
    }
}
