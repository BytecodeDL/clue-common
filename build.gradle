plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

project.group = 'org.clyze'

wrapper {
    gradleVersion = '6.7'
}

repositories {
    mavenCentral()
}

def groovyVersion='3.0.6'
dependencies {
    implementation "org.codehaus.groovy:groovy:${groovyVersion}"
    implementation "org.codehaus.groovy:groovy-xml:${groovyVersion}"
    api 'commons-io:commons-io:2.4'             // Apache 2.0
    api 'log4j:log4j:1.2.14'                    // Logging implementation, Apache 2.0
    api 'org.apache.ivy:ivy:2.5.0'              // Apache Ivy (for downloading inputs from Maven repos), Apache 2.0
    api 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'  // MPL 1.1

    testImplementation 'org.spockframework:spock-core:2.0-M3-groovy-3.0'
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    if (!System.getProperty('java.version').startsWith('1.'))
        options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

release {
    //use defaults, see https://github.com/researchgate/gradle-release
}

tasks.release.doLast {
    println "WARNING: Releasing clue-common affects both public doop and doop-nexgen, please check that everything works."
}

afterReleaseBuild.dependsOn publish

if (project.hasProperty('artifactory_user')) {
    publishing {
        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }
                //Always publish to the public releases repo
                url "${artifactory_contextUrl}/libs-public-release-local"
            }
        }
    }
}
